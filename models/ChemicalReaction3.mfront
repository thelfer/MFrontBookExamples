@DSL Model;
@Model ChemicalReaction3;
@Author Thomas Helfer;
@Date 09/07/2022;

@Import "ChemicalReaction-common.mfront";

//! numerical parameter of the generalized mid-point rule
@Parameter real theta = 0.5;
theta.setEntryName("Theta");

@Function ChemicalReaction {
  constexpr auto zero = quantity<real, 0, 0, 0, 0, 0, 0, 1>{};
  const auto T_mts = T_1 * (1 - theta) + theta * T;
  const auto k1_mts = k01 * exp(-T_mts / Ta1);
  const auto k2_mts = k02 * exp(-T_mts / Ta2);
  const auto B = dt * (k2_mts * cb_1 - k1_mts * ca_1);
  const auto K = dt * (k1_mts + k2_mts);
  ca = ca_1 + B / (1 + K * theta);
  cb = ca_1 + cb_1 - ca;
  // imposing positivity of the molar concentrations
  if(cb < zero){
    cb = zero;
    ca = ca_1 + cb_1;
  }
  if(ca < zero){
    ca = zero;
    cb = ca_1 + cb_1;
  }
}